plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'signing'

    // lombok
    id 'io.freefair.lombok' version '6.5.0.3'
}

apply from: System.getProperty("user.home") + '/Github/LayGit/publish.gradle'

group 'com.laylib'
version '1.0.0'

repositories {
    maven{ url 'https://maven.aliyun.com/nexus/content/groups/public/'}
    mavenCentral()
}

dependencies {

    // Utils
    implementation 'org.yaml:snakeyaml:1.30'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.slf4j:slf4j-api:1.7.36'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.slf4j:slf4j-simple:1.7.36'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'jIntl'
                description = 'a Java internationalization components'
                url = 'https://jintl.laylib.com'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Lay'
                        email = 'jserlay@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/LayGit/jIntl'
                    connection = 'scm:git:git://github.com/LayGit/jIntl.git'
                    developerConnection = 'scm:git:ssh://git@github.com:LayGit/jIntl.git'
                }
            }
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    options.addStringOption("charset", "UTF-8")
}